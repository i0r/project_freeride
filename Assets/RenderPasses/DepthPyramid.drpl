lib DepthPyramid {
    properties {
        uint2       TextureSize;
        float2      CameraPlanes; // .x : Near Plane; .y : Far Plane
        float4x4    Projection;
    }
    
    resources {
        Texture2D DepthBuffer {
            swizzle = float;
        }
        
        Texture2D CurrentDepthMip {
            swizzle = unorm float2;
        }
        
        RWTexture2D ReducedDepthMip {
            swizzle = unorm float2;
        }
        
        RWTexture2D DownsampledepthMip {
            swizzle = float;
        }
        
        sampler SamplerPoint {
            format = Point;
        }
    }
    
    shared { 
        #include "GeometryUtils.hlsli"
     
        static const uint ReductionTGSize = 16;      
        static const uint NumThreads = ReductionTGSize * ReductionTGSize;
        
        groupshared float2 depthSamples[NumThreads];
    }
    
    shader DepthReductionInitialCS {
        float minDepth = 1.0f;
        float maxDepth = 0.0f;

        uint2 samplePos = $SV_GroupID.xy * ReductionTGSize + $SV_GroupThreadID.xy;
        samplePos = min(samplePos, TextureSize - 1);

        float depthSample = ConvertFromReversedDepth( DepthBuffer[samplePos], g_ClippingPlanes.x );

        if(depthSample < 1.0f)
        {
            minDepth = min( minDepth, depthSample );
            maxDepth = max( maxDepth, depthSample );
        }
        
        // Store in shared memory
        depthSamples[$SV_GroupIndex] = float2( minDepth, maxDepth );
        GroupMemoryBarrierWithGroupSync();

        // Reduce
        [unroll]
        for( uint s = NumThreads / 2; s > 0; s >>= 1 )
        {
            if ( $SV_GroupIndex < s )
            {
                depthSamples[$SV_GroupIndex].x = min( depthSamples[$SV_GroupIndex].x, depthSamples[$SV_GroupIndex + s].x );
                depthSamples[$SV_GroupIndex].y = max( depthSamples[$SV_GroupIndex].y, depthSamples[$SV_GroupIndex + s].y );
            }

            GroupMemoryBarrierWithGroupSync();
        }

        if ( $SV_GroupIndex == 0 )
        {
            minDepth = depthSamples[0].x;
            maxDepth = depthSamples[0].y;
           
            ReducedDepthMip[$SV_GroupID.xy] = float2( minDepth, maxDepth );
        }
    }
    
    shader DepthReductionCS {
        uint2 samplePos = $SV_GroupID.xy * ReductionTGSize + $SV_GroupThreadID.xy;
        samplePos = min( samplePos, TextureSize - 1 );
        
        float minDepth = CurrentDepthMip[samplePos].x;
        float maxDepth = CurrentDepthMip[samplePos].y;

        if ( maxDepth == 0.0f ) {
            maxDepth = 1.0f;
        }
        
        // Store in shared memory
        depthSamples[$SV_GroupIndex] = float2( minDepth, maxDepth );
        GroupMemoryBarrierWithGroupSync();

        // Reduce
        [unroll]
        for( uint s = NumThreads / 2; s > 0; s >>= 1 )
        {
            if ( $SV_GroupIndex < s )
            {
                depthSamples[$SV_GroupIndex].x = min( depthSamples[$SV_GroupIndex].x, depthSamples[$SV_GroupIndex + s].x );
                depthSamples[$SV_GroupIndex].y = max( depthSamples[$SV_GroupIndex].y, depthSamples[$SV_GroupIndex + s].y );
            }

            GroupMemoryBarrierWithGroupSync();
        }

        if ( $SV_GroupIndex == 0 )
        {
            minDepth = depthSamples[0].x;
            maxDepth = depthSamples[0].y;
            
            ReducedDepthMip[$SV_GroupID.xy] = float2( minDepth, maxDepth );
        }
    }
    
    shader DownscaleDepthCS {
        if ( all( $SV_DispatchThreadID.xy < TextureSize.xy ) ) {
          float4 depths = DepthBuffer.Gather( SamplerPoint, ( $SV_DispatchThreadID.xy + 0.5f ) / TextureSize.xy );
          
          DownsampledepthMip[$SV_DispatchThreadID.xy] = max( max( depths.x, depths.y ), max( depths.z, depths.w ) );
        }
    }
    
    pass DepthReductionMip0 {
        compute       = DepthReductionInitialCS;
        dispatch      = { 16, 16, 1 };
    }
    
    pass DepthReduction {
        compute       = DepthReductionCS;
        dispatch      = { 16, 16, 1 };
    }
    
    pass DepthDownsample {
        compute       = DownscaleDepthCS;
        dispatch      = { 16, 16, 1 };
    }
}
